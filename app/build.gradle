apply plugin: "com.android.application"

apply plugin: "kotlin-android"

apply plugin: "kotlin-android-extensions"

apply plugin: "kotlin-kapt"

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "be_nao.mvvm_base"
        minSdkVersion 22
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }


    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def dagger_version = "2.16"
def retrofit_ver = "2.3.0"
def navigation_ver = "2.1.0"
def okhttp3_ver = "3.10.0"

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "com.google.android.material:material:1.0.0"
    testImplementation "junit:junit:4.12"

    // Kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // AndroidX
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    // Navigation
    implementation "androidx.lifecycle:lifecycle-extensions:$navigation_ver"
    implementation "androidx.navigation:navigation-fragment:$navigation_ver"
    implementation "androidx.navigation:navigation-ui:$navigation_ver"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_ver"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_ver"

    // Dagger 2
    // Until Jetifier fixes Dagger 2.17 related issue,
    // Temporally uses 2.16
    // https://issuetracker.google.com/issues/115738511
    // https://github.com/google/dagger/issues/1245
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Retrofit2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_ver"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_ver"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_ver"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_ver"

    // OkHttp3
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_ver"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp3_ver" // ログ出力

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.12"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxbinding3:rxbinding:3.0.0"

    //Timber
    implementation "com.jakewharton.timber:timber:4.7.1"

    implementation "com.facebook.conceal:conceal:2.0.2@aar"
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.34.2"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint